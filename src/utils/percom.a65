PTR		=	0
LFTMARGIN	=	$52

DDEVIC		=	$300
DUNIT		=	$301
DCOMND		=	$302
DSTATS		=	$303
DBUFLO		=	$304
DBUFHI		=	$305
DTIMLO		=	$306
DBYTLO		=	$308
DBYTHI		=	$309
DAUX1		=	$30A
DAUX2		=	$30B

ICCOM		=	$342
ICSTA		=	$344
ICBAL		=	$344
ICBAH		=	$345
ICBLL		=	$348
ICBLH		=	$349
ICAX1		=	$34A
ICAX2		=	$34B

		
CIOV		=	$E456
SIOV		=	$E459
		
CCOPEN		=	3
CGETB		=	7
CPUTB		=	11
CCLOSE		=	12

CLS		=	$7D
EOL		=	$9B
;		
		.org	$2000
;		
START:		lda	#0
		sta	LFTMARGIN
		lda	#<SALLYMSG
		sta	PTR
		lda	#>SALLYMSG
		sta	PTR+1
		jsr	PRTSTR
		
		jsr	GETKEY
		cmp	#'D'
		beq	DODUMP
		cmp	#'G'
		beq	DOGOTO
		cmp	#'P'
		beq	DOPERCOM
		cmp	#'U'
		beq	DOUPLOAD
		cmp	#'V'
		beq	DOVERSION
		cmp	#'X'
		beq	EXIT
		bne	START
EXIT:		rts

DODUMP:		jmp	DUMP
DOGOTO:		jmp	GOTO
DOUPLOAD:	jmp	UPLOAD
DOVERSION:	jmp	VERSION

DOPERCOM:	lda	#<PERTXT0
		sta	PTR
		lda	#>PERTXT0
		sta	PTR+1
		jsr	PRTSTR
		
		jsr	GETKEY
		jsr	PRINT
		
		cmp	#'1'
		bcc	START
		cmp	#'8'+1
		bcs	START
		sbc	#'1'-2
		sta	DUNIT
		jsr	READPER				;read percom block
		tya	
		bmi	DOPERCOM			;iterate if error
		
DOPERCOM1:	jsr	SHOWPER				;display percom block
		
		jsr	GETKEY
		cmp	#'T'
		beq	TRACKS
		cmp	#'S'
		beq	SECS
		cmp	#'H'
		beq	HEADS
		cmp	#'D'
		beq	DENSITY
		cmp	#'B'
		beq	BYTES
		cmp	#'W'
		beq	WRITEPERC
		cmp	#'X'
		beq	DOPERCOM
		bne	DOPERCOM1

;
;
;
WRITEPERC:	jsr	WRITEPER
		jmp	DOPERCOM1

TRACKS:		lda	#<MSGTRK
		sta	PTR
		lda	#>MSGTRK
		sta	PTR+1
		jsr	PRTSTR
		jsr	getbyte
		sta	PERTRK
		jmp	DOPERCOM1

SECS:		lda	#<MSGSEC
		sta	PTR
		lda	#>MSGSEC
		sta	PTR+1
		jsr	PRTSTR
		jsr	getbyte
		sta	PERSEC+1
		lda	WORD+1
		sta	PERSEC
		jmp	DOPERCOM1

HEADS:		lda	#<MSGHEA
		sta	PTR
		lda	#>MSGHEA
		sta	PTR+1
		jsr	PRTSTR
		jsr	getbyte
		sta	PERHEA
		jmp	DOPERCOM1
DENSITY:
BYTES:		lda	#<MSGBYT
		sta	PTR
		lda	#>MSGBYT
		sta	PTR+1
		jsr	PRTSTR
		jsr	getbyte
		sta	PERBYT+1
		lda	WORD+1
		sta	PERBYT
		jmp	DOPERCOM1 
;
;
;		
SHOWPER:	lda	#<PERTXT1
		sta	PTR
		lda	#>PERTXT1
		sta	PTR+1
		jsr	PRTSTR
		
		lda	DUNIT				;DUNIT
		ldx	#0
		jsr	PRTWORD
		
		jsr	PRTSTR				;TRKS
		lda	PERCOM
		jsr	PRTWORD
		
		jsr	PRTSTR				;STEP
		lda	PERCOM+1
		jsr	PRTWORD
		
		jsr	PRTSTR				;SEC/TRK
		lda	PERCOM+3
		ldx	PERCOM+2
		jsr	PRTWORD
		
		jsr	PRTSTR				;SIDES
		lda	PERCOM+4
		clc	
		adc	#1
		jsr	PRTWORD
		
		jsr	PRTSTR				;DESITY
		lda	PERCOM+5
		jsr	PRTWORD
		
		jsr	PRTSTR				;BYT/SEC
		lda	PERCOM+7
		ldx	PERCOM+6
		jsr	PRTWORD
		
		jsr	PRTSTR				;SELECTED
		lda	PERCOM+8
		jsr	PRTWORD
		
		jsr	PRTSTR				;SERRATE
		lda	PERCOM+9
		jsr	PRTWORD
		
		jmp	PRTSTR
		

WRITEPER:	lda	#$80
		sta	DSTATS
		lda	#'O'
		bne	READPER1
READPER:	lda	#$40
		sta	DSTATS
		lda	#'N'
READPER1:	sta	DCOMND
		lda	#<PERCOM
		sta	DBUFLO
		lda	#>PERCOM
		sta	DBUFHI
		lda	#'1'
		sta	DDEVIC
		lda	#<12
		sta	DBYTLO
		lda	#>12
		sta	DBYTHI
		jmp	SIOV

SETADR:		lda	#0
		sta	DSTATS
		lda	#'S'
		sta	DCOMND
		lda	#'Z'
		sta	DDEVIC
		lda	#1
		sta	DUNIT
		lda	#<BUFFER
		sta	DBUFLO
		lda	#>BUFFER
		sta	DBUFHI
		lda	#3
		sta	DBYTLO
		lda	#0
		sta	DBYTHI
		lda	SALLYADR
		sta	DAUX1
		lda	SALLYADR+1
		sta	DAUX2
		jmp	SIOV

PUTRAM:		lda	#'W'	
		sta	DCOMND
		lda	#128
		sta	DSTATS
		lda	ATADR
		sta	DBUFLO
		lda	ATADR+1
		sta	DBUFHI
		lda	#0
		sta	DAUX1
		sta	DBYTLO
		lda	#1
		sta	DAUX2
		sta	DBYTHI
		jmp	SIOV

GETRAM:		lda	#'R'
		sta	DCOMND
		lda	#64
		sta	DSTATS
		lda	#0
		sta	DAUX1
		sta	DBYTLO
		lda	#1
		sta	DAUX2
		sta	DBYTHI
		jmp	SIOV
		
GOTOADR:	lda	#'G'
		sta	DCOMND
		lda	#0
		sta	DAUX1
		sta	DBYTLO
		sta	DAUX2
		sta	DBYTHI
		jmp	SIOV
;
;
;
GOTO:		jsr	getadr
		cmp	#'X'
		beq	GOTOEND
		lda	WORD
		sta	SALLYADR
		lda	WORD+1
		sta	SALLYADR+1
		jsr	SETADR
		jsr	GOTOADR
GOTOEND:	jmp	START
;
;
;
VERSION:	lda	#0
		sta	SALLYADR
		sta	SALLYADR+1
		jsr	SETADR
		jsr	GETRAM
		
		clc
		lda	BUFFER+$3a
		adc	BUFFER+$40
		sta	SALLYADR
		lda	BUFFER+$3b
		adc	BUFFER+$41		
		sta	SALLYADR+1
		jsr	SETADR
		jsr	GETRAM
		
		ldx	#12
VERSION1:	lda	BUFFER,x
		jsr	PRINT
		inx
		cpx	#12+16
		bne	VERSION1
		jsr	NEWLINE
		jsr	GETKEY		
		jmp	START
		
;
;
;
UPLOAD:		jsr	openfile
;		tya
;		jsr	puthex
		
		jsr	readfile
;		tya
;		jsr	puthex

		jsr	closefile
;		tya
;		jsr	puthex
		
		ldx	#$10
		lda	ICBLH,x
		sta	SALLYLEN+1
		lda	ICBLL,x
		sta	SALLYLEN

		lda	#0
		sta	SALLYADR
		sta	ATADR
		lda	#$20
		sta	SALLYADR+1
		lda	#$30
		sta	ATADR+1
		
upoload1:	lda	SALLYLEN+1
		jsr	puthex
		lda	SALLYLEN
		jsr	puthex
		lda	#32
		jsr	PRINT
		
		jsr	SETADR
		jsr	PUTRAM
		tya
		jsr	puthex
		jsr	NEWLINE
		
		inc	SALLYADR+1
		inc	ATADR+1
		dec	SALLYLEN+1
		bpl	upoload1
		
		jsr	GETKEY
		jmp	START

;
;
;
openfile:	ldx	#$10
		lda	#CCOPEN
		sta	ICCOM,x
		lda	#<sallycom
		sta	ICBAL,x
		lda	#>sallycom
		sta	ICBAH,x
		lda	#0
		sta	ICBLL,x
		lda	#1
		sta	ICBLH,x
		lda	#4
		sta	ICAX1,x
		lda	#0
		sta	ICAX2,x
		jmp	CIOV
		
readfile:	ldx	#$10
		lda	#CGETB
		sta	ICCOM,x
		lda	#$00
		sta	ICBAL,x
		lda	#$30
		sta	ICBAH,x
		lda	#$ff
		sta	ICBLL,x
		lda	#$ff
		sta	ICBLH,x
		lda	#4
		sta	ICAX1,x
		lda	#0
		sta	ICAX2,x
		jmp	CIOV	
		
closefile:	ldx	#$10
		lda	#CCLOSE
		sta	ICCOM,x
		jmp	CIOV
		
;
;
;
dumpbuf:	ldx	#0
		ldy	#16
		sty	WORD

		lda	SALLYADR
		sta	DUMPADR
		lda	SALLYADR+1
		sta	DUMPADR+1
		
dumpbuf2:	jsr	dumpadr
dumpbuf1:	lda	BUFFER,x
		jsr	puthex
		inx
		txa
		and	#15
		cmp	#8
		bne	dumpbuf3
		lda	#32
		jsr	PRINT
dumpbuf3:	dey
		bne	dumpbuf1
		jsr	NEWLINE
		ldy	#16
		dec	WORD
		bne	dumpbuf2
		rts
		
dumpadr:	lda	DUMPADR+1
		jsr	puthex
		lda	DUMPADR
		jsr	puthex
		lda	#':'
		jsr	PRINT
		clc
		lda	DUMPADR
		adc	#16
		sta	DUMPADR
		bcc	dumpadr1
		inc	DUMPADR+1
dumpadr1:	rts

PRTSTR:		ldy	#0
		lda	(PTR),Y
		beq	INCPTR
		jsr	PRINT
		jsr	INCPTR
		jmp	PRTSTR
		
INCPTR:		inc	PTR
		bne	INCPTR1
		inc	PTR+1
INCPTR1:	rts	
	
;
;
;	
DUMP:		jsr	getadr
		cmp	#'X'
		bne	DUMP1
		jmp	START
DUMP1:		cmp	#EOL
		bne	DUMP2
		inc	SALLYADR+1
		jmp	DUMP3
DUMP2:		lda	WORD
		sta	SALLYADR
		lda	WORD+1
		sta	SALLYADR+1
DUMP3:		jsr	NEWLINE
		jsr	SETADR
		jsr	GETRAM
		jsr	dumpbuf
		jmp	DUMP

getadr:		lda	#<msgadr
		sta	PTR
		lda	#>msgadr
		sta	PTR+1
		jsr	PRTSTR
		
		lda	#0
		sta	WORD
		sta	WORD+1
		lda	#4
		sta	CNT

getadr2:	jsr	getnibble
		cmp	#16
		bcs	getadr3
		ldx	#3
getadr4:	asl	WORD
		rol	WORD+1
		dex
		bpl	getadr4
		adc	WORD
		sta	WORD
		bcc	getadr1
		inc	WORD+1
getadr1:	dec	CNT
		bne	getadr2
getadr3:	rts

getnibble:	jsr	GETKEY
		cmp	#EOL
		beq	getnibex
		cmp	#'X'
		beq	getnibex
		cmp	#'0'
		bcc	getnibble
		cmp	#'9'+1
		bcc	getnib09
		
		cmp	#'A'
		bcc	getnibble
		cmp	#'F'+1
		bcs	getnibble
		jsr	PRINT
		sec
		sbc	#'A'-10
		rts
getnib09:	jsr	PRINT
		sec
		sbc	#'0'
getnibex:	rts

;
; reads a byte value given in decimal
;
getbyte:	ldx	#3
		stx	PACKDEC
		lda	#0
		sta	WORD
		sta	WORD+1
getbyte1:	jsr	getnum
		bmi	getbyteex
		lda	WORD
		asl
		rol	WORD+1
		asl
		rol	WORD+1
		adc	WORD
		sta	WORD
		bcc	getbyte2
		inc	WORD+1
getbyte2:	asl
		sta	WORD
		rol	WORD+1
		tya
		adc	WORD
		sta	WORD
		bcc	getbyte3
		inc	WORD+1
getbyte3:	dec	PACKDEC
		bne	getbyte1
getbyteex:	lda	WORD
		rts
		
getnum:		jsr	GETKEY
		cmp	#EOL
		beq	getnumex
		cmp	#'0'
		bcc	getnum
		cmp	#'9'+1
		bcs	getnum
		jsr	PRINT
		sec
		sbc	#'0'
getnumex:	tay
		rts
		
;			
puthex:		pha
		pha
		lsr
		lsr
		lsr
		lsr
		jsr	putnib
		pla
		and	#$0f
		jsr	putnib
		pla
		rts
		
putnib:		cmp	#10
		bcc	putnib1
		adc	#6
putnib1:	adc	#'0'
		jmp	PRINT

NEWLINE:	lda	#EOL
PRINT:		pha	
		txa	
		pha	
		tya	
		pha	
		ldx	#CPUTB
		stx	ICCOM
		tsx	
		lda	$103,X
		ldx	#0
		stx	ICBLL
		stx	ICBLH
		jsr	CIOV
		pla	
		tay	
		pla	
		tax	
		pla	
		rts	
		
GETKEY:		lda	$E425
		pha	
		lda	$E424
		pha	
		rts	
;		
PRTWORD:	jsr	WORD2D
		ldx	#0
		ldy	#0
PRTWRD1:	lda	PACKDEC,Y
		lsr	
		lsr	
		lsr	
		lsr	
		jsr	PRTNIB
		cpy	#2
		bne	PRTWRD2
		inx	
PRTWRD2:	lda	PACKDEC,Y
		and	#15
		jsr	PRTNIB
		iny	
		cpy	#3
		bne	PRTWRD1
		ldx	#0
		jmp	NEWLINE
		
PRTNIB:		bne	PRTNIB1
		cpx	#0
		bne	PRTNIB1
		rts	
		
PRTNIB1:	inx	
		clc	
		adc	#'0'
		jmp	PRINT
		
		
WORD2D:		stx	WORD+1
		sta	WORD
		
		ldx	#2
		lda	#0
WORD2D1:	sta	PACKDEC,X
		dex	
		bpl	WORD2D1
		
		sed	
		ldx	#16
		
WORD2D3:	asl	WORD
		rol	WORD+1
		
		ldy	#2
WORD2D2:	lda	PACKDEC,Y
		adc	PACKDEC,Y
		sta	PACKDEC,Y
		dey	
		bpl	WORD2D2
		
		dex	
		bne	WORD2D3
		
		cld	
		rts	
;		
; VAR + CONST	
;		
		
PERCOM:
PERTRK:		.byte	0
PERSTP:		.byte	0
PERSEC:		.word	0
PERHEA:		.byte	0
PERDEN:		.byte	0
PERBYT:		.word	0
PERSTA:		.byte	0
PERRAT:		.byte	0
PERX01:		.byte	0
PERX02:		.byte	0

WORD:		.word	0
PACKDEC:	.byte	0,0,0
		
SALLYMSG:	.byte	CLS
		.byte	"******************", EOL
		.byte	"* Sally 2 tools  *", EOL
		.byte	"******************", EOL
		.byte	EOL
		.byte	"D - Dump memory",EOL
		.byte	"G - Goto address",EOL
		.byte	"P - Percom Block Config",EOL
		.byte	"U - upload sally2.com",EOL
		.byte	"V - Show Sally 2 version", EOL
		.byte	"X - EXIT", EOL
		.byte	0
		
PERTXT0:	.byte	EOL
		.byte	"Display PERCOM for"
		.byte	" Unit (1-8)?"
		.byte	0
		
PERTXT1:	.byte	EOL
		.byte	"****************"
		.byte	EOL
		.byte	"* PERCOM BLOCK *"
		.byte	EOL
		.byte	"****************"
		.byte	EOL
		.byte	"Unit   : "
		.byte	0
		
MSGTRK:		.byte	"Tracks : "
		.byte	0
		.byte	"Step   : "
		.byte	0
MSGSEC:		.byte	"Sec/Trk: "
		.byte	0
MSGHEA:		.byte	"Heads  : "
		.byte	0
MSGDEN:		.byte	"Density: "
		.byte	0
MSGBYT:		.byte	"Byt/Sec: "
		.byte	0
		.byte	"Select : "
		.byte	0
		.byte	"SerRate: "
		.byte	0
		.byte	'T'+128, "racks ", 'S'+128, "ecs ", 'H'+128, "eads ", 'D'+128, "ensity ", 'B'+128, "ytes", EOL
		.byte	'W'+128, "rite  ", 'X'+128, "xit", EOL
		.byte	0

sallycom:	.byte	"D:SALLY2.COM",EOL
msgadr:		.byte	"address:",0
ATADR:		.word	0
SALLYADR:	.word	0
SALLYLEN:	.word	0
DUMPADR:	.word	0
CNT:		.byte	0
BUFFER:		.byte	0