PTR		=	0
LFTMARGIN	=	$52

DVSTAT		=	$2ea

DDEVIC		=	$300
DUNIT		=	$301
DCOMND		=	$302
DSTATS		=	$303
DBUFLO		=	$304
DBUFHI		=	$305
DTIMLO		=	$306
DBYTLO		=	$308
DBYTHI		=	$309
DAUX1		=	$30A
DAUX2		=	$30B

ICCOM		=	$342
ICSTA		=	$344
ICBAL		=	$344
ICBAH		=	$345
ICBLL		=	$348
ICBLH		=	$349
ICAX1		=	$34A
ICAX2		=	$34B
		
CIOV		=	$E456
SIOV		=	$E459
		
CCOPEN		=	3
CGETB		=	7
CPUTB		=	11
CCLOSE		=	12

CREAD		=	64
CWRITE		=	128

CLS		=	$7D
EOL		=	$9B
;		
		.org	$2000
;		
START:		lda	#0
		sta	LFTMARGIN

		lda	#<SALLYMSG
		sta	PTR
		lda	#>SALLYMSG
		sta	PTR+1
		jsr	PRTSTR

		lda	#<MSGDUNIT
		sta	PTR
		lda	#>MSGDUNIT
		sta	PTR+1
		jsr	PRTSTR
		
		lda	DUNIT				;DUNIT
		ldx	#0
		jsr	PRTWORD
		
		jsr	GETKEY
		cmp	#'D'
		beq	DODUMP
		cmp	#'F'
		beq	DOFORMAT
		cmp	#'G'
		beq	DOGOTO
		cmp	#'N'
		beq	DONACK
		cmp	#'P'
		beq	DOPERCOM
		cmp	#'R'
		beq	DORESET
		cmp	#'S'
		beq	DOGETSTAT
		cmp	#'T'
		beq	DOTESTSEC
		cmp	#'U'
		beq	DOUPLOAD
		cmp	#'V'
		beq	DOVERSION
		cmp	#'X'
		beq	EXIT
		bne	START
EXIT:		rts

DODUMP:		jmp	DUMP
DOFORMAT:	jmp	FORMAT
DOGOTO:		jmp	GOTO
DONACK:		jmp	NACK
DOGETSTAT:	jmp	GETSTAT
DORESET:	jmp	RESET
DOTESTSEC:	jmp	TESTSEC
DOUPLOAD:	jmp	UPLOAD
DOVERSION:	jmp	VERSION

DOPERCOM:	lda	#<PERTXT0
		sta	PTR
		lda	#>PERTXT0
		sta	PTR+1
		jsr	PRTSTR
				
		jsr	GETKEY
		jsr	PRINT
		
		cmp	#'1'
		bcs	DOPERCOM3
		jmp	START
DOPERCOM3:	cmp	#'8'+1
		bcc	DOPERCOM2
		jmp	START
DOPERCOM2:	sbc	#'1'-2
		sta	DUNIT
		
		jsr	READPER				;read percom block
		tya	
		bmi	DOPERCOM			;iterate if error
		
DOPERCOM1:	jsr	SHOWPER				;display percom block
		
		jsr	GETKEY
		cmp	#'T'
		beq	TRACKS
		cmp	#'S'
		beq	SECS
		cmp	#'H'
		beq	HEADS
		cmp	#'D'
		beq	DENSITY
		cmp	#'B'
		beq	BYTES
		cmp	#'W'
		beq	WRITEPERC
		cmp	#'X'
		beq	DOPERCOM
		bne	DOPERCOM1

;
;
;
WRITEPERC:	jsr	WRITEPER
		jmp	DOPERCOM1

TRACKS:		lda	#<MSGTRK
		sta	PTR
		lda	#>MSGTRK
		sta	PTR+1
		jsr	PRTSTR
		jsr	getbyte
		sta	PERTRK
		jmp	DOPERCOM1

SECS:		lda	#<MSGSEC
		sta	PTR
		lda	#>MSGSEC
		sta	PTR+1
		jsr	PRTSTR
		jsr	getbyte
		sta	PERSEC+1
		lda	WORD+1
		sta	PERSEC
		jmp	DOPERCOM1

HEADS:		lda	#<MSGHEA
		sta	PTR
		lda	#>MSGHEA
		sta	PTR+1
		jsr	PRTSTR
		jsr	getbyte
		sta	PERHEA
		jmp	DOPERCOM1
DENSITY:	lda	#<MSGDEN
		sta	PTR
		lda	#>MSGDEN
		sta	PTR+1
		jsr	PRTSTR
		jsr	getbyte
		sta	PERDEN
		jmp	DOPERCOM1
BYTES:		lda	#<MSGBYT
		sta	PTR
		lda	#>MSGBYT
		sta	PTR+1
		jsr	PRTSTR
		jsr	getbyte
		sta	PERBYT+1
		lda	WORD+1
		sta	PERBYT
		jmp	DOPERCOM1 
;
;
;		
SHOWPER:	lda	#<PERTXT1
		sta	PTR
		lda	#>PERTXT1
		sta	PTR+1
		jsr	PRTSTR
		
		lda	DUNIT				;DUNIT
		ldx	#0
		jsr	PRTWORD
		
		jsr	PRTSTR				;TRKS
		lda	PERCOM
		jsr	PRTWORD
		
		jsr	PRTSTR				;STEP
		lda	PERCOM+1
		jsr	PRTWORD
		
		jsr	PRTSTR				;SEC/TRK
		lda	PERCOM+3
		ldx	PERCOM+2
		jsr	PRTWORD
		
		jsr	PRTSTR				;SIDES
		lda	PERCOM+4
;		clc	
;		adc	#1
		jsr	PRTWORD
		
		jsr	PRTSTR				;DESITY
		lda	PERCOM+5
		jsr	PRTWORD
		
		jsr	PRTSTR				;BYT/SEC
		lda	PERCOM+7
		ldx	PERCOM+6
		jsr	PRTWORD
		
		jsr	PRTSTR				;SELECTED
		lda	PERCOM+8
		jsr	PRTWORD
		
		jsr	PRTSTR				;SERRATE
		lda	PERCOM+9
		jsr	PRTWORD
		
		jmp	PRTSTR
		

WRITEPER:	lda	#$80
		sta	DSTATS
		lda	#'O'
		bne	READPER1
READPER:	lda	#$40
		sta	DSTATS
		lda	#'N'
READPER1:	sta	DCOMND
		lda	#<PERCOM
		sta	DBUFLO
		lda	#>PERCOM
		sta	DBUFHI
		lda	#'1'
		sta	DDEVIC
		lda	#<12
		sta	DBYTLO
		lda	#>12
		sta	DBYTHI
		jmp	CALLSIO

SETADR:		lda	#0
		sta	DSTATS
		lda	#'S'
		sta	DCOMND
		lda	#'Z'
		sta	DDEVIC
		lda	#1
		sta	DUNIT
		lda	#0
		sta	DBYTLO
		sta	DBYTHI
		lda	SALLYADR
		sta	DAUX1
		lda	SALLYADR+1
		sta	DAUX2
		jmp	SIOV

PUTRAM:		jsr	SETADR
		lda	#'W'	
		sta	DCOMND
		lda	#CWRITE
		sta	DSTATS
		bne	GETRAM1

GETRAM:		jsr	SETADR
		lda	#'R'
		sta	DCOMND
		lda	#CREAD
		sta	DSTATS
GETRAM1:	lda	#<BUFFER
		sta	DBUFLO
		lda	#>BUFFER
		sta	DBUFHI
		lda	#0
		sta	DAUX1
		sta	DBYTLO
		lda	#1
		sta	DAUX2
		sta	DBYTHI
		jmp	SIOV
		
GOTOADR:	jsr	SETADR
		lda	#'Z'
		sta	DDEVIC
		lda	#1
		sta	DUNIT
		lda	#'G'
		sta	DCOMND
		lda	#0
		sta	DSTATS
		lda	SALLYADR
		sta	DAUX1
		lda	SALLYADR+1
		sta	DAUX2
		lda	#0
		sta	DBYTLO
		sta	DBYTHI
		jmp	SIOV
;
;
;
GOTO:		jsr	getadr
		cmp	#'X'
		beq	GOTOEND
		lda	WORD
		sta	SALLYADR
		lda	WORD+1
		sta	SALLYADR+1
		jsr	GOTOADR
		lda	SALLYADR+1
		jsr	puthex
		lda	SALLYADR
		jsr	puthex
		jsr	GETKEY
GOTOEND:	jmp	START
;
;
;
TESTSEC:	lda	#'1'
		sta	DDEVIC
		lda	#'R'
		sta	DCOMND
		lda	#<BUFFER
		sta	DBUFLO
		lda	#>BUFFER
		sta	DBUFHI
		lda	#<3000				;start at sector 1
		sta	DAUX1
		lda	#>3000
		sta	DAUX2
TESTSEC1:	lda	#CREAD
		sta	DSTATS
		lda	#128				;standard 128 bytes/sec
		sta	DBYTLO
		lda	#0
		sta	DBYTHI
		lda	PERDEN				;double density?
		and	#4
		beq	TESTSEC4			;no, jump to readsection
		lda	DAUX2				;yes, check if DAUX2 > 0
		bne	TESTSEC2			;yes, switch to 256 bytes/sec
		lda	DAUX1				;no, check if sector 1-3
		cmp	#4
		bcc	TESTSEC4			;yes, just read 128 bytes
TESTSEC2:	asl	DBYTLO
		rol	DBYTHI
TESTSEC4:	lda	DAUX2
		jsr	puthex
		lda	DAUX1
		jsr	puthex
		jsr	NEWLINE
		jsr	CALLSIO
		bmi	TESTSEC3			;if error, quit read loop
		inc	DAUX1				;increment DAUX1/2
		bne	TESTSEC1
		inc	DAUX2	
		bne	TESTSEC1
TESTSEC3:	jsr	GETKEY
		jmp	START		

;
;
;
NACK:		lda	#'1'
		sta	DDEVIC
		lda	#'X'
		sta	DCOMND
		lda	#0
		sta	DSTATS
		sta	DBYTLO
		sta	DBYTHI
		jsr	CALLSIO
		jmp	START
		
FORMAT:		lda	#'1'
		sta	DDEVIC
		lda	#'!'
		sta	DCOMND
		lda	#CREAD
		sta	DSTATS
		lda	#<BUFFER
		sta	DBUFLO
		lda	#>BUFFER
		sta	DBUFHI
		lda	DTIMLO
		pha
		lda	#$ff
		sta	DTIMLO
		lda	#128
		sta	DBYTLO
		lda	#0
		sta	DBYTHI
		lda	PERDEN
		and	#4
		beq	FORMAT1
		asl	DBYTLO
		rol	DBYTHI
FORMAT1:	jsr	CALLSIO
		pla
		sta	DTIMLO
		jmp	START

GETSTAT:	lda	#'1'
		sta	DDEVIC
		lda	#'S'
		sta	DCOMND
		lda	#CREAD
		sta	DSTATS
		lda	#<DVSTAT
		sta	DBUFLO
		lda	#>DVSTAT
		sta	DBUFHI
		lda	#4
		sta	DBYTLO
		lda	#0
		sta	DBYTHI
		jsr	CALLSIO
		
		ldx	#0
GETSTAT1:	lda	DVSTAT,x
		jsr	puthex
		jsr	SPACE
		inx
		cpx	#4
		bne	GETSTAT1
		jsr	GETKEY
		jmp	START
		
CALLSIO:	jsr	SIOV
		bpl	CALLSIO1
		tya
		jsr	puthex
		jsr	SPACE
		ldy	#128
CALLSIO1:	rts
		
VERSION:	lda	#0
		sta	SALLYADR
		sta	SALLYADR+1
		jsr	GETRAM
		
		sec
		lda	BUFFER+$2f
		sbc	#$1f
		sta	SALLYADR
		lda	BUFFER+$30
		sbc	#0
		sta	SALLYADR+1
		jsr	GETRAM
		
		ldx	#0
VERSION1:	lda	BUFFER,x
		jsr	PRINT
		inx
		cpx	#16
		bne	VERSION1
		jsr	NEWLINE
		jsr	GETKEY		
		jmp	START
		
;
; uploads sally2.com to $2000 using a 256 buffer @ BUFFER
;
UPLOAD:		jsr	openfile

		lda	#0
		sta	SALLYADR
		lda	#$80
		sta	SALLYADR+1
		
upload1:	jsr	readfile			;read a page from file
		lda	ICBLH,x
		jsr	puthex
		lda	ICBLL,x
		jsr	puthex
		jsr	SPACE
		
		jsr	PUTRAM				;put to Sally RAM
		tya
		jsr	puthex
		tay
		bmi	uploadex
		
		jsr	SPACE
		
		lda	SALLYADR+1
		jsr	puthex
		lda	SALLYADR
		jsr	puthex
		
		jsr	NEWLINE
		
		inc	SALLYADR+1
		ldx	#$10
		lda	ICBLH,x				;check if 256 were loaded
		bne	upload1				;loop if yes

		jsr	closefile
		
		lda	#0
		sta	SALLYADR
		lda	#$80
		sta	SALLYADR+1
		jsr	GETRAM
		
		sec
		lda	BUFFER+$2f
		sbc	#$0f
		sta	SALLYADR
		lda	BUFFER+$30
		sbc	#0
		adc	#$80-1
		sta	SALLYADR+1
		
		jsr	GETKEY
		jsr	GOTOADR
		
uploadex:	jmp	START

RESET:		lda	#$39
		sta	SALLYADR
		lda	#0
		sta	SALLYADR+1		
		jsr	GOTOADR
		jmp	START

;
;
;
openfile:	ldx	#$10
		lda	#CCOPEN
		sta	ICCOM,x
		lda	#<sallycom
		sta	ICBAL,x
		lda	#>sallycom
		sta	ICBAH,x
		lda	#0
		sta	ICBLL,x
		lda	#1
		sta	ICBLH,x
		lda	#4
		sta	ICAX1,x
		lda	#0
		sta	ICAX2,x
		jmp	CIOV
		
readfile:	ldx	#$10
		lda	#CGETB
		sta	ICCOM,x
		lda	#<BUFFER
		sta	ICBAL,x
		lda	#>BUFFER
		sta	ICBAH,x
		lda	#$00
		sta	ICBLL,x
		lda	#$01
		sta	ICBLH,x
		lda	#4
		sta	ICAX1,x
		lda	#0
		sta	ICAX2,x
		jmp	CIOV	
		
closefile:	ldx	#$10
		lda	#CCLOSE
		sta	ICCOM,x
		jmp	CIOV
		
;
;
;
dumpbuf:	ldx	#0
		ldy	#8
		lda	#16
		sta	WORD

		lda	SALLYADR
		sta	DUMPADR
		lda	SALLYADR+1
		sta	DUMPADR+1
		
dumpbuf2:	jsr	dumpadr
dumpbuf1:	lda	BUFFER,x
		jsr	puthex
		inx
		jsr	SPACE
dumpbuf3:	dey
		bne	dumpbuf1
		
		txa
		sec
		sbc	#8
		tax

		ldy	#8
		lda	#'|'
		jsr	PRINT
		
dumpbuf5:	lda	BUFFER,x
		and	#$7f
		cmp	#32
		bcc	dumpbuf4
		cmp	#$7d
		bcc	dumpbuf6
dumpbuf4:	lda	#'.'
dumpbuf6:	jsr	PRINT
		inx
		dey
		bne	dumpbuf5
		
		lda	#'|'
		jsr	PRINT
		jsr	NEWLINE
		
		ldy	#8
		dec	WORD
		bne	dumpbuf2
		rts
		
dumpadr:	lda	DUMPADR+1
		jsr	puthex
		lda	DUMPADR
		jsr	puthex
		lda	#':'
		jsr	PRINT
		clc
		lda	DUMPADR
		adc	#8
		sta	DUMPADR
		bcc	dumpadr1
		inc	DUMPADR+1
dumpadr1:	rts

PRTSTR:		ldy	#0
		lda	(PTR),Y
		beq	INCPTR
		jsr	PRINT
		jsr	INCPTR
		jmp	PRTSTR
		
INCPTR:		inc	PTR
		bne	INCPTR1
		inc	PTR+1
INCPTR1:	rts	
	
;
;
;
CLEARBUF:	ldx	#0
CLEARBUF1:	sta	BUFFER,x
		inx
		bne	CLEARBUF1
		rts
;
;
;
DUMP:		jsr	getadr
		cmp	#'X'
		bne	DUMP1
		jmp	START
DUMP1:		cmp	#EOL
		bne	DUMP2
		clc
		lda	SALLYADR
		adc	#128
		sta	SALLYADR
		bcc	DUMP4
		inc	SALLYADR+1
DUMP4:		jmp	DUMP3

DUMP2:		lda	WORD
		sta	SALLYADR
		lda	WORD+1
		sta	SALLYADR+1
DUMP3:		jsr	NEWLINE
		jsr	GETRAM
		jsr	dumpbuf
		jmp	DUMP

getadr:		lda	#<msgadr
		sta	PTR
		lda	#>msgadr
		sta	PTR+1
		jsr	PRTSTR
		
		lda	#0
		sta	WORD
		sta	WORD+1
		lda	#4
		sta	CNT

getadr2:	jsr	getnibble
		cmp	#16
		bcs	getadr3
		ldx	#3
getadr4:	asl	WORD
		rol	WORD+1
		dex
		bpl	getadr4
		adc	WORD
		sta	WORD
		bcc	getadr1
		inc	WORD+1
getadr1:	dec	CNT
		bne	getadr2
getadr3:	rts

getnibble:	jsr	GETKEY
		cmp	#EOL
		beq	getnibex
		cmp	#'X'
		beq	getnibex
		cmp	#'0'
		bcc	getnibble
		cmp	#'9'+1
		bcc	getnib09
		
		cmp	#'A'
		bcc	getnibble
		cmp	#'F'+1
		bcs	getnibble
		jsr	PRINT
		sec
		sbc	#'A'-10
		rts
getnib09:	jsr	PRINT
		sec
		sbc	#'0'
getnibex:	rts

;
; reads a byte value given in decimal
;
getbyte:	ldx	#3
		stx	PACKDEC
		lda	#0
		sta	WORD
		sta	WORD+1
getbyte1:	jsr	getnum
		bmi	getbyteex
		lda	WORD
		asl
		rol	WORD+1
		asl
		rol	WORD+1
		adc	WORD
		sta	WORD
		bcc	getbyte2
		inc	WORD+1
getbyte2:	asl
		sta	WORD
		rol	WORD+1
		tya
		adc	WORD
		sta	WORD
		bcc	getbyte3
		inc	WORD+1
getbyte3:	dec	PACKDEC
		bne	getbyte1
getbyteex:	lda	WORD
		rts
		
getnum:		jsr	GETKEY
		cmp	#EOL
		beq	getnumex
		cmp	#'0'
		bcc	getnum
		cmp	#'9'+1
		bcs	getnum
		jsr	PRINT
		sec
		sbc	#'0'
getnumex:	tay
		rts
		
;			
puthex:		pha
		pha
		lsr
		lsr
		lsr
		lsr
		jsr	putnib
		pla
		and	#$0f
		jsr	putnib
		pla
		rts
		
putnib:		cmp	#10
		bcc	putnib1
		adc	#6
putnib1:	adc	#'0'
		jmp	PRINT

SPACE:		lda	#32
		bne	PRINT
NEWLINE:	lda	#EOL
PRINT:		pha	
		txa	
		pha	
		tya	
		pha	
		ldx	#CPUTB
		stx	ICCOM
		tsx	
		lda	$103,X
		ldx	#0
		stx	ICBLL
		stx	ICBLH
		jsr	CIOV
		pla	
		tay	
		pla	
		tax	
		pla	
		rts	
		
GETKEY:		lda	$E425
		pha	
		lda	$E424
		pha	
		rts	
;		
PRTWORD:	jsr	WORD2D
		ldx	#0
		ldy	#0
PRTWRD1:	lda	PACKDEC,Y
		lsr	
		lsr	
		lsr	
		lsr	
		jsr	PRTNIB
		cpy	#2
		bne	PRTWRD2
		inx	
PRTWRD2:	lda	PACKDEC,Y
		and	#15
		jsr	PRTNIB
		iny	
		cpy	#3
		bne	PRTWRD1
		ldx	#0
		jmp	NEWLINE
		
PRTNIB:		bne	PRTNIB1
		cpx	#0
		bne	PRTNIB1
		rts	
		
PRTNIB1:	inx	
		clc	
		adc	#'0'
		jmp	PRINT
		
		
WORD2D:		stx	WORD+1
		sta	WORD
		
		ldx	#2
		lda	#0
WORD2D1:	sta	PACKDEC,X
		dex	
		bpl	WORD2D1
		
		sed	
		ldx	#16
		
WORD2D3:	asl	WORD
		rol	WORD+1
		
		ldy	#2
WORD2D2:	lda	PACKDEC,Y
		adc	PACKDEC,Y
		sta	PACKDEC,Y
		dey	
		bpl	WORD2D2
		
		dex	
		bne	WORD2D3
		
		cld	
		rts	
;		
; VAR + CONST	
;		
		
PERCOM:
PERTRK:		.byte	0
PERSTP:		.byte	0
PERSEC:		.word	0
PERHEA:		.byte	0
PERDEN:		.byte	0
PERBYT:		.word	0
PERSTA:		.byte	0
PERRAT:		.byte	0
PERX01:		.byte	0
PERX02:		.byte	0

WORD:		.word	0
PACKDEC:	.byte	0,0,0
		
SALLYMSG:	.byte	CLS
		.byte	"******************", EOL
		.byte	"* Sally 2 tools  *", EOL
		.byte	"******************", EOL
		.byte	EOL
		.byte	"D - Dump memory", EOL
		.byte	"G - Goto address", EOL
		.byte	"F - Format", EOL
		.byte	"N - Test NACK (Cmd 'X')", EOL
		.byte	"P - Percom Block Config", EOL
		.byte	"R - Reset Sally", EOL
		.byte	"S - Get Status", EOL
		.byte	"T - Test (read) all sectors", EOL
		.byte	"U - upload sally2.com", EOL
		.byte	"V - Show Sally 2 version", EOL
		.byte	"X - EXIT", EOL
		.byte	0
		
PERTXT0:	.byte	EOL
		.byte	"Display PERCOM for"
		.byte	" Unit (1-8 / x)?"
		.byte	0
		
PERTXT1:	.byte	EOL
		.byte	"****************"
		.byte	EOL
		.byte	"* PERCOM BLOCK *"
		.byte	EOL
		.byte	"****************"
		.byte	EOL
MSGDUNIT:	.byte	"Unit       : "
		.byte	0
		
MSGTRK:		.byte	"Tracks     : "
		.byte	0
		.byte	"Step       : "
		.byte	0
MSGSEC:		.byte	"Sec/Trk    : "
		.byte	0
MSGHEA:		.byte	"Heads (0-1): "
		.byte	0
MSGDEN:		.byte	"Density    : "
		.byte	0
MSGBYT:		.byte	"Byt/Sec    : "
		.byte	0
		.byte	"Select     : "
		.byte	0
		.byte	"SerRate    : "
		.byte	0
		.byte	'T'+128, "racks ", 'S'+128, "ecs ", 'H'+128, "eads ", 'D'+128, "ensity ", 'B'+128, "ytes", EOL
		.byte	'W'+128, "rite  ", 'E', 'x'+128, "it", EOL
		.byte	0

sallycom:	.byte	"D:SALLY2.COM",EOL
msgadr:		.byte	"address:",0
ATADR:		.word	0
SALLYADR:	.word	0
SALLYLEN:	.word	0
DUMPADR:	.word	0
CNT:		.byte	0
BUFFER:		.byte	0